Platform boxes[] = new Platform[400];

int counter = 0;

float zPos;

void setup() {
  size(480, 480, P3D);
  background(0);
  smooth();

  for (int i = 0; i < boxes.length; i++) {
    boxes[i] = new Platform();
  }

  for (int i = 12; i <= width-12; i+=24) {
    for (int j = 12; j <= 480-12; j+=24) {

      counter++;

      boxes[counter-1].position(i, j);
    }
  }
}

void draw() {

  translate(0, 250, 0);
  rotateX(radians(45));
  translate(0, -450, 0);

  background(0);
 // lights();
//directionalLight(175, 175, 175, 0, 1, 0);
  spotLight(255, 255, 255, width/2, 960/2, 400, 0, 0, -1, PI/4, 2);

float my = mouseY;

  for (int i = 0; i < boxes.length; i++) {
    boxes[i].update(mouseX, my);
    boxes[i].display();
  }
  
  if(mousePressed){
    zPos += (0 - zPos) * .1;
  }else{
    zPos += (-115 - zPos) * .1;
  }

  //println(boxes[63].getAngle());
  
  

  fill(255);
  pushMatrix();
  translate(mouseX, my, zPos);
  sphere(25);
  popMatrix();
}

class Platform {

  float x, y, z, an;

  float affect = 125;

  Platform() {

    x = 0;
    y = 0;
  }
void position(float tx, float ty) {

    x = tx;
    y = ty;
    z = -100;

    an = 0;
  }

  void update(float ux, float uy) {

    float mdist = dist(ux, uy, x, y);

    float lift = map(mdist, 0, affect, PI, 0);
    
    float easing = map(mdist, 0, affect, .25, .01);
    
    if(easing < .01){
      easing = .01;
    }
    
    if(easing > 1){
      easing = 1;
    }

    if (mdist < affect) {
      an += (lift - an) * easing;
    } else {
      an += (0 - an) * easing;
    }
    
    if(mousePressed){
      an += (0 - an) * .1;
    }


    z = -100 + ((50 * cos(an))*map(zPos,0,-115,0,1));
  }

  void display() {
    noStroke();
    fill(0, map(an,0,PI,255,100), 0);
    pushMatrix();
    translate(x, y, z); 
    box(22);
    popMatrix();
  }

  float getAngle() {
    return(an);
  }
}
